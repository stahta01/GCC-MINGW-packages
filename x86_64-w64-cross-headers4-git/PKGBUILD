# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

# This package forked from mingw-w64-cross-headers-git from https://github.com/msys2/MSYS2-packages

_base_pkg_prefix=/mingw32/opt
_mingw_suff=x86_64-w64-cross
_target="x86_64-w64-mingw32"
_build_and_host="i686-w64-mingw32"

_basename=headers
_base_pkg_version=4
_realname=${_basename}
pkgbase=${_mingw_suff}-${_basename}${_base_pkg_version}-git
pkgname=("${_mingw_suff}-${_realname}-git")
replaces=("${_mingw_suff}-${_realname}")
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
pkgdesc="MinGW-w64 headers for cross-compiler"
_ver_base=4.0.0
pkgver=4.0.0.4514.ae0e22cd
pkgrel=1
arch=('i686' 'x86_64')
url="http://mingw-w64.sourceforge.net"
license=('custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
makedepends=('git')
options=('!strip' '!libtool' '!emptydirs' '!buildflags')
_git_branch="v4.x"
source=("${_realname}"::"git://git.code.sf.net/p/mingw-w64/mingw-w64#branch=$_git_branch")
sha256sums=(SKIP)

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    msg "Configuring ${_target} headers"
    mkdir -p ${srcdir}/headers-${_target} && cd ${srcdir}/headers-${_target}
    ${srcdir}/${_realname}/mingw-w64-headers/configure \
    --build=${_build_and_host} \
    --host=${_target} \
    --prefix=${_base_pkg_prefix}/${_target} \
    --enable-sdk=all \
    --enable-secure-api
}

package() {
    msg "Installing ${_target} headers"
    cd ${srcdir}/headers-${_target}
    make DESTDIR=${pkgdir} install

    rm ${pkgdir}${_base_pkg_prefix}/${_target}/include/pthread_signal.h
    rm ${pkgdir}${_base_pkg_prefix}/${_target}/include/pthread_time.h
    rm ${pkgdir}${_base_pkg_prefix}/${_target}/include/pthread_unistd.h
}
