# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

_realname=binutils2.23
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}")
pkgver=2.23.2
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('any')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=("${_mingw_suff}-toolchain")
depends=("${_mingw_suff}-libiconv" "${_mingw_suff}-zlib")
checkdepends=('dejagnu' 'bc')
makedepends=("${_mingw_suff}-libiconv" "${_mingw_suff}-zlib")
options=('staticlibs' '!distcc' '!ccache')
#install=binutils.install
source=("https://mirrors.kernel.org/sources.redhat.com/binutils/releases/binutils-${pkgver}.tar.bz2"
        'binutils-2.23.2-texinfo-5.0.patch')
md5sums=('4f8fa651e35ef262edc01d60fb45702e'
         '34e439ce23213a91e2af872dfbb5094c')

prepare() {
  cd ${srcdir}/binutils-${pkgver}

  # http://sourceware.org/git/?p=binutils.git;a=patch;h=e02bf935
  # http://sourceware.org/git/?p=binutils.git;a=patch;h=935f8542
  patch -p1 -i ${srcdir}/binutils-2.23.2-texinfo-5.0.patch

  #do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  if [ "${CARCH}" = "x86_64" ]; then
    local _conf='--enable-64-bit-bfd'
  else
    local _conf=''
    LDFLAGS+=" -Wl,--large-address-aware -lntdll"
  fi
  ${srcdir}/binutils-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX}/opt \
    --disable-werror \
    --enable-lto \
    --with-sysroot=${MINGW_PREFIX}/opt \
    --with-libiconv-prefix=${MINGW_PREFIX} \
    $_conf \
    --disable-nls \
    --disable-rpath \
    --disable-multilib

  make
}

check() {
  cd build-${MINGW_CHOST}

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  # Remove unwanted files
  rm -rf ${pkgdir}${MINGW_PREFIX}/opt/share

  # Add some useful headers
  # install -m644 ${srcdir}/binutils-${pkgver}/include/libiberty.h ${pkgdir}${MINGW_PREFIX}/include
  # install -m644 ${srcdir}/binutils-${pkgver}/include/demangle.h ${pkgdir}${MINGW_PREFIX}/include
}
