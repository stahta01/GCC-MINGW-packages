# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Alexey Borzenkov <snaury@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: wirx6 <wirx654@gmail.com>
# Contributor: Liu Hao <lh_mouse@126.com>

_enable_ada=no
_enable_fortran=no
_enable_objc=no

_basename=gcc
_major_version=7
_realname=${_basename}
_make_program_major_version=
_make_program_suffix= # -${_make_program_major_version}
_makedependsname=${_basename}
pkgbase=mingw-w64-${_basename}${_major_version}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-libs"
         $([[ "$_enable_fortran" == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}-libgfortran")
         $([[ "$_enable_fortran" == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}-fortran")
         $([[ "$_enable_ada" == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}-ada")
         $([[ "$_enable_objc" == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}-objc")
)
pkgver=${_major_version}.4.0
pkgrel=1
pkgdesc="GCC for the MinGW-w64"
arch=('any')
url="https://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
makedepends=("${MINGW_PACKAGE_PREFIX}-${_realname}"
             $([[ "$_enable_ada" == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}-ada")
             "${MINGW_PACKAGE_PREFIX}-binutils"
             "${MINGW_PACKAGE_PREFIX}-crt"
             "${MINGW_PACKAGE_PREFIX}-headers"
             "${MINGW_PACKAGE_PREFIX}-libiconv"
             "${MINGW_PACKAGE_PREFIX}-windows-default-manifest"
             "${MINGW_PACKAGE_PREFIX}-winpthreads"
             "${MINGW_PACKAGE_PREFIX}-zlib")
#checkdepends=('dejagnu')
optdepends=()
options=('staticlibs' '!emptydirs') # '!strip' 'debug')
# mpc version 1.2.0 requires at least mpfr 4.1.0
# mpc Version 1.1.0 requires at least gmp 5.0.0, mpfr 3.0.0
# mpc Version 0.9   requires at least gmp 4.3.2, mpfr 2.4.2
# mpc Version 0.7   requires at least gmp 4.2
MPFR_VERSION=mpfr-3.1.4
GMP_VERSION=gmp-6.1.0
MPC_VERSION=mpc-1.0.3
ISL_VERSION=isl-0.18
noextract=("${MPFR_VERSION}.tar.bz2" "${GMP_VERSION}.tar.bz2" "${MPC_VERSION}.tar.gz" "${ISL_VERSION}.tar.bz2")
_sourcedir=${_basename}-${pkgver}
source=("https://ftp.gnu.org/gnu/gcc/${_basename}-${pkgver}/${_basename}-${pkgver}.tar.xz"{,.sig}
        "ftp://gcc.gnu.org/pub/gcc/infrastructure/${MPFR_VERSION}.tar.bz2"
        "ftp://gcc.gnu.org/pub/gcc/infrastructure/${GMP_VERSION}.tar.bz2"
        "ftp://gcc.gnu.org/pub/gcc/infrastructure/${MPC_VERSION}.tar.gz"
        "ftp://gcc.gnu.org/pub/gcc/infrastructure/${ISL_VERSION}.tar.bz2"
        'isl-0.14.1-no-undefined.patch'
        "002-Relocate-libintl.patch"
        "003-Windows-Follow-Posix-dir-exists-semantics-more-close.patch"
        "004-Windows-Use-not-in-progpath-and-leave-case-as-is.patch"
        "005-Windows-Don-t-ignore-native-system-header-dir.patch"
        "006-gcc-6-branch-Windows-New-feature-to-allow-overriding.patch"
        "007-Build-EXTRA_GNATTOOLS-for-Ada.patch"
        "008-Prettify-linking-no-undefined.patch"
        "009-gcc-make-xmmintrin-header-cplusplus-compatible-depre.patch"
        "010-Fix-using-large-PCH.patch"
        "011-Enable-shared-gnat-implib.patch"
        "014-gcc-6-branch-clone_function_name_1-Retain-any-stdcall-suffix.patch"
        "0016-disable-weak-refs-in-libstdc++.patch"
        "0017-diagnostic-color.patch"
        '020-libgomp-Don-t-hard-code-MS-printf-attributes.patch'
        "0100-gcc-4.8-libstdc++export.patch"
        "0110-gcc-4.7-stdthreads.patch"
        "0130-libstdc++-in-out.patch"
        "0140-gcc-7-Enable-std-experimental-filesystem.patch")
sha256sums=('eddde28d04f334aec1604456e536416549e9b1aa137fc69204e65eb0c009fe51'
            'SKIP'
            'd3103a80cdad2407ed581f3618c4bed04e0c92d1cf771a65ead662cc397f7775'
            '498449a994efeba527885c10405993427995d3f86b8768d8cdf8d9dd7c6b73e8'
            '617decc6ea09889fb08ede330917a00b16809b8db88c29c31bfbb49cbf88ecc3'
            '6b8b0fd7f81d0a957beb3679c81bbb34ccc7568d5682844d8924424a0dadcb1b'
            '83655a7202f0a0dcce1782d4b365252bf1ad12a522b7ad82ab578ee5ec46433b'
            '83e9c6f76f728ae3e2f2eabb588b0d6cea7a3eda03cd5e77aed9d613143b7348'
            '6033acda690786397059536787a6577e1a9f2b39f8a66276821917066094f43c'
            '388f423a67e4be6f547ca7e340ff8ee4c78b1ed83f3fd698daa409e309911807'
            '86fc77d5a6c137a9aca65ad6ba56653fb41b05636b763ce2c0b0b14d617c9de4'
            'd1e10cd2ef5594c34acead334bb93af500e00699ba673edc8916da22a572677d'
            '9c4dcedc156b7f31d91b9862c4936b6840ddf7a6a2d1e075d080cf7536c6422a'
            'ec6800816632234afc35ce40542a6aab3ee76e8514a7d0cb4171fd874353ecd7'
            '0eaee0e1201f4f85ea20756432a9d51531e0e859f6f121276684e2b63b2ee315'
            '0decbbebda7ceac91da5d3dec9b7330f406a4358fa150560f08ff6f54739035f'
            '8faff8e503a617f283270f411399a8bcacda3ab62b4072dedf117e108af4844a'
            '60a58ed41389691a68ef4b7d47a0328df4d28d26e6c680a6b06b31191481ca65'
            '09f27e0dae8d962f2a46a33a9891f2d14303629bb40f91ed8c5824c90da653a9'
            'e33e8f5d1f93250714ddf9d0e09d7e1d5e67ba024f8ec23e96064b34d09ce33d'
            '2c9b80eebd42ac85cb4b4134617f7eae02de19cc59cc399e4720c6100db430e2'
            '21191b4fd57ce5f230fcc97b4d9ae31bdc387d7c7c8e39436aa7e4268d278d3d'
            '5e0fc1437ce0b357e78d440692e3a30a7905a5f360a67928a95b14ec8d45365b'
            'dce731024e7df92f43c88e15a180a49cea2e588302d7e2a8623df12c892b4fc2'
            '37e6e6d6b7a2ff27a807bfb390fb4a91b8f67cf38cad345c51d0a8011a15efc1')

_threads="posix"

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f ${_fname} ]; then
      rm -rf ${_fname}
    fi
  done
}

_extract_to_gcc_folder() {
    local tarfile="$1"
    local subfolder="$(echo "$tarfile" | sed 's/-.*$//')"
    if [ ! -d  "${_sourcedir}/$subfolder" ]; then
        echo "Extracting ${tarfile} to ${_sourcedir}/$subfolder"
        mkdir -p "${_sourcedir}/$subfolder"
        tar -x --strip-components=1 -f "$tarfile" -C "${_sourcedir}/$subfolder"
    fi
}
# =========================================== #

prepare() {
  cd ${srcdir}

  rm -fr ${_sourcedir}/isl || true

  _extract_to_gcc_folder       $MPFR_VERSION.tar.bz2
  _extract_to_gcc_folder       $GMP_VERSION.tar.bz2
  _extract_to_gcc_folder       $MPC_VERSION.tar.gz
  _extract_to_gcc_folder       $ISL_VERSION.tar.bz2

  cd ${srcdir}/${_sourcedir}/isl
  patch -Nbp1 -i ${srcdir}/isl-0.14.1-no-undefined.patch
  autoreconf -fi  # needed for version 0.18 and likely all lower versions

  cd ${srcdir}/${_sourcedir}
  del_file_exists intl/canonicalize.c intl/canonicalize.h \
    intl/relocatex.c intl/relocatex.h \
    libstdc++-v3/src/filesystem/fs-posix.h \
    libstdc++-v3/src/filesystem/fs-win32.h \
    gcc/testsuite/gfortran.dg/list_read_14.f90
  apply_patch_with_msg \
     002-Relocate-libintl.patch \
     003-Windows-Follow-Posix-dir-exists-semantics-more-close.patch \
     004-Windows-Use-not-in-progpath-and-leave-case-as-is.patch \
     005-Windows-Don-t-ignore-native-system-header-dir.patch \
     006-gcc-6-branch-Windows-New-feature-to-allow-overriding.patch \
     007-Build-EXTRA_GNATTOOLS-for-Ada.patch \
     008-Prettify-linking-no-undefined.patch \
     009-gcc-make-xmmintrin-header-cplusplus-compatible-depre.patch \
     010-Fix-using-large-PCH.patch \
     011-Enable-shared-gnat-implib.patch

  apply_patch_with_msg \
     014-gcc-6-branch-clone_function_name_1-Retain-any-stdcall-suffix.patch \
     0016-disable-weak-refs-in-libstdc++.patch \
     0017-diagnostic-color.patch \
     020-libgomp-Don-t-hard-code-MS-printf-attributes.patch

  apply_patch_with_msg \
    0100-gcc-4.8-libstdc++export.patch \
    0110-gcc-4.7-stdthreads.patch

  apply_patch_with_msg 0130-libstdc++-in-out.patch

  apply_patch_with_msg 0140-gcc-7-Enable-std-experimental-filesystem.patch

  # do not expect ${prefix}/mingw symlink - this should be superceded by
  # 0004-Windows-Don-t-ignore-native-system-header-dir.patch .. but isn't!
  sed -i 's/${prefix}\/mingw\//${prefix}\//g' configure

  # change hardcoded /mingw prefix to the real prefix .. isn't this rubbish?
  # it might work at build time and could be important there but beyond that?!
  local MINGW_NATIVE_PREFIX=$(cygpath -am ${MINGW_PREFIX}/${MINGW_CHOST})
  sed -i "s#\\/mingw\\/#${MINGW_NATIVE_PREFIX//\//\\/}\\/#g" gcc/config/i386/mingw32.h
}

_package_common() {
  # Use without "with-gcc-major-version-only" configuration option
  _gcc_driver_version=`eval "cat ${srcdir}/${_sourcedir}/gcc/BASE-VER"`

  _pkg_prefix=${MINGW_PREFIX}

  echo "_gcc_driver_version := ${_gcc_driver_version}"
  echo "_pkg_prefix := ${_pkg_prefix}"
}

build() {
  _package_common

  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  case "${CARCH}" in
    i686)
      local _conf="--disable-sjlj-exceptions --with-dwarf2"
      LDFLAGS+=" -Wl,--large-address-aware"
      local _arch=i686
    ;;

    x86_64)
      local _arch=x86-64
      local _conf=""
    ;;
  esac

  local _languages="c,c++,lto"
  if [ "$_enable_fortran" == "yes" ]; then
    _languages+=",fortran"
  fi
  if [ "$_enable_ada" == "yes" ]; then
    _languages+=",ada"
  fi
  if [ "$_enable_objc" == "yes" ]; then
    _languages+=",objc,obj-c++"
  fi

  ../${_sourcedir}/configure \
    --prefix=${_pkg_prefix} \
    --with-local-prefix=${MINGW_PREFIX}/local \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-native-system-header-dir=${MINGW_PREFIX}/${MINGW_CHOST}/include \
    --libexecdir=${_pkg_prefix}/lib \
    --with-arch=${_arch} \
    --with-tune=generic \
    --enable-languages=${_languages} \
    --enable-shared --enable-static \
    --enable-libatomic \
    --enable-threads=${_threads} \
    --enable-graphite \
    --enable-fully-dynamic-string \
    --enable-libstdcxx-time=yes \
    --enable-libstdcxx-filesystem-ts=yes \
    --disable-libstdcxx-pch \
    --disable-libstdcxx-debug \
    --disable-isl-version-check \
    --enable-lto \
    --enable-libgomp \
    --disable-multilib \
    --enable-checking=release \
    --disable-rpath \
    --disable-win32-registry \
    --disable-nls \
    --disable-werror \
    --disable-symvers \
    --with-libiconv \
    --with-system-zlib \
    --with-pkgversion="Rev${pkgrel}, Built by stahta01 -- Tim S" \
    --with-bugurl="https://github.com/stahta01/GCC-MINGW-packages/issues" \
    --with-gnu-as --with-gnu-ld \
    ${_conf}

   # --enable-bootstrap \

  # While we're debugging -fopenmp problems at least.
  # .. we may as well not strip anything.
  if check_option "strip" "n"; then
    sed -i 's,^STRIP = .*$,STRIP = true,g'                   Makefile
    sed -i 's,^STRIP_FOR_TARGET=.*$,STRIP_FOR_TARGET=true,g' Makefile
  fi

  make all

  rm -rf ${srcdir}${_pkg_prefix}

  make -j1 DESTDIR=${srcdir} install
  if [ "$_enable_ada" == "yes" ]; then
    mv ${srcdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/adalib/*.dll ${srcdir}${_pkg_prefix}/bin/
  fi
}

package_mingw-w64-gcc-libs() {
  pkgdesc="GNU Compiler Collection (libraries) for MinGW-w64"
  depends=("${MINGW_PACKAGE_PREFIX}-libwinpthread")

  # Licensing information

  # Part of the package is GCCRLE, part is LGPL2+, see README generation below.
  # Since the packaged GCCRLE libraries are also GPL3+, and LGPL2+ is compatible
  # with GPL3+, the whole package can be redistributed under GPL3+.
  license=(GPL3+ partial:'GCCRLE' partial:'LGPL2+')

  # We explain the licensing in this generated README file
  mkdir -p "${pkgdir}${_pkg_prefix}/share/licenses/${_realname}-libs"
  cat << ENDFILE > "${pkgdir}${_pkg_prefix}/share/licenses/${_realname}-libs/README"
The libgcc, libssp, libstdc++, libgomp and libatomic libraries are covered by
GPL3+ with the GCC Runtime Library Exception. The libquadmath library is covered
by LGPL2+. The package as a whole can be redistributed under GPL3+.
ENDFILE

  # License files
  install -Dm644 "${srcdir}/${_sourcedir}/COPYING3"        "${pkgdir}${_pkg_prefix}/share/licenses/${_realname}-libs/COPYING3"
  install -Dm644 "${srcdir}/${_sourcedir}/COPYING.LIB"     "${pkgdir}${_pkg_prefix}/share/licenses/${_realname}-libs/COPYING.LIB"
  install -Dm644 "${srcdir}/${_sourcedir}/COPYING.RUNTIME" "${pkgdir}${_pkg_prefix}/share/licenses/${_realname}-libs/COPYING.RUNTIME"

  mkdir -p ${pkgdir}${_pkg_prefix}/bin

  cd ${srcdir}${_pkg_prefix}
  # libitm* and libvtv* are disbled until fixed
  cp bin/{libatomic*,libgcc*,libgomp*,libquadmath*,libssp*,libstdc*}.dll ${pkgdir}${_pkg_prefix}/bin/
}

package_mingw-w64-gcc() {
  pkgdesc="GNU Compiler Collection (C,C++,OpenMP) for MinGW-w64"
  depends=("${MINGW_PACKAGE_PREFIX}-binutils"
           "${MINGW_PACKAGE_PREFIX}-crt"
           "${MINGW_PACKAGE_PREFIX}-headers"
           "${MINGW_PACKAGE_PREFIX}-libiconv"
           "${MINGW_PACKAGE_PREFIX}-${_realname}-libs=${pkgver}-${pkgrel}"
           "${MINGW_PACKAGE_PREFIX}-windows-default-manifest"
           "${MINGW_PACKAGE_PREFIX}-winpthreads"
           "${MINGW_PACKAGE_PREFIX}-zlib")
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}-base")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-base")

  mkdir -p ${pkgdir}${_pkg_prefix}/{bin,include,lib,share}

  cd ${srcdir}${_pkg_prefix}
  cp bin/cpp.exe                                        ${pkgdir}${_pkg_prefix}/bin/
  cp bin/gcc.exe                                        ${pkgdir}${_pkg_prefix}/bin/
  cp bin/gcc.exe                                        ${pkgdir}${_pkg_prefix}/bin/cc.exe
  cp bin/gcc-ar.exe                                     ${pkgdir}${_pkg_prefix}/bin/
  cp bin/gcc-nm.exe                                     ${pkgdir}${_pkg_prefix}/bin/
  cp bin/gcc-ranlib.exe                                 ${pkgdir}${_pkg_prefix}/bin/
  cp bin/gcov.exe                                       ${pkgdir}${_pkg_prefix}/bin/
  cp bin/gcov-tool.exe                                  ${pkgdir}${_pkg_prefix}/bin/
  cp bin/c++.exe                                        ${pkgdir}${_pkg_prefix}/bin/
  cp bin/g++.exe                                        ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-c++.exe                         ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-g++.exe                         ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-gcc-${_gcc_driver_version}.exe               ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-gcc.exe                         ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-gcc-ar.exe                      ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-gcc-nm.exe                      ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-gcc-ranlib.exe                  ${pkgdir}${_pkg_prefix}/bin/

  #cp bin/{libgcc*,libgomp*,libquadmath*,libssp*,libstdc*}.dll ${pkgdir}${_pkg_prefix}/bin/
  mkdir -p ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/*.h        ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/ssp     ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include-fixed   ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/install-tools   ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/cc1.exe            ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/collect2.exe       ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/crt*.o             ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/liblto*            ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/libgcov*           ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/lto*.exe           ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/cc1plus.exe        ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/

  cp lib/libatomic*                                      ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/libgcc*                                         ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/libgcc*            ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/

  cp lib/libgomp*                                        ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  # cp lib/libitm*                                         ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/libquadmath*                                    ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/libssp*                                         ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  # cp lib/libvtv*                                         ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/libstdc++*                                      ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/libsupc++*                                      ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/

  #mkdir -p ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/lib
  #cp ${srcdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/lib/libgcc_s.a ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  #cp lib/gcc/${MINGW_CHOST}/lib/libgcc_s.a                ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/lib/

  #cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/c++      ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/
  cp -r include/c++                                       ${pkgdir}${_pkg_prefix}/include/
  #cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/libstdc++*         ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  #cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/libsupc++*         ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/

  mkdir -p ${pkgdir}${_pkg_prefix}/share/{doc,info,locale,man}
  #cp -r share/doc/gcc-${_gcc_driver_version} ${pkgdir}${_pkg_prefix}/share/doc/
  cp share/info/cpp.info*                                ${pkgdir}${_pkg_prefix}/share/info/
  cp share/info/cppinternals.info*                       ${pkgdir}${_pkg_prefix}/share/info/
  cp share/info/gcc.info*                                ${pkgdir}${_pkg_prefix}/share/info/
  cp share/info/gccinstall.info*                         ${pkgdir}${_pkg_prefix}/share/info/
  cp share/info/gccint.info*                             ${pkgdir}${_pkg_prefix}/share/info/
  # cp share/info/libitm.info*                             ${pkgdir}${_pkg_prefix}/share/info/
  cp share/info/libgomp.info*                            ${pkgdir}${_pkg_prefix}/share/info/
  cp share/info/libquadmath.info*                        ${pkgdir}${_pkg_prefix}/share/info/

  #cp share/locale/* ${pkgdir}${_pkg_prefix}/share/locale/
  mkdir -p ${pkgdir}${_pkg_prefix}/share/gcc-${_gcc_driver_version}/python
  cp -r share/gcc-${_gcc_driver_version}/python/libstdcxx             ${pkgdir}${_pkg_prefix}/share/gcc-${_gcc_driver_version}/python/
  mkdir -p ${pkgdir}${_pkg_prefix}/share/man/man1
  cp share/man/man1/cpp.1*                               ${pkgdir}${_pkg_prefix}/share/man/man1/
  cp share/man/man1/gcc.1*                               ${pkgdir}${_pkg_prefix}/share/man/man1/
  cp share/man/man1/gcov.1*                              ${pkgdir}${_pkg_prefix}/share/man/man1/
  cp share/man/man7/fsf-funding.7*                       ${pkgdir}${_pkg_prefix}/share/man/man1/
  cp share/man/man7/gfdl.7*                              ${pkgdir}${_pkg_prefix}/share/man/man1/
  cp share/man/man7/gpl.7*                               ${pkgdir}${_pkg_prefix}/share/man/man1/
  cp share/man/man1/g++.1*                               ${pkgdir}${_pkg_prefix}/share/man/man1/

  # install plugins for binutils
  mkdir -p ${pkgdir}${_pkg_prefix}/lib/bfd-plugins
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/*plugin*.dll       ${pkgdir}${_pkg_prefix}/lib/bfd-plugins/
}

package_mingw-w64-gcc-libgfortran() {
  pkgdesc="GNU Compiler Collection (libgfortran) for MinGW-w64"
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}-libs=${pkgver}-${pkgrel}")

  mkdir -p ${pkgdir}${_pkg_prefix}/bin
  cd ${srcdir}${_pkg_prefix}
  cp bin/libgfortran*.dll                                ${pkgdir}${_pkg_prefix}/bin/
}

package_mingw-w64-gcc-fortran() {
  pkgdesc="GNU Compiler Collection (Fortran) for MinGW-w64"
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}=${pkgver}-${pkgrel}"
           "${MINGW_PACKAGE_PREFIX}-${_realname}-libgfortran=${pkgver}-${pkgrel}")

  mkdir -p ${pkgdir}${_pkg_prefix}/{bin,lib,share}

  cd ${srcdir}${_pkg_prefix}
  cp bin/gfortran.exe ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-gfortran.exe ${pkgdir}${_pkg_prefix}/bin/

  #cp bin/libgfortran*.dll ${pkgdir}${_pkg_prefix}/bin/

  mkdir -p ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/finclude       ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/f951.exe          ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/libcaf_single.a   ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/libgfortran*                                   ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  mkdir -p ${pkgdir}${_pkg_prefix}/share/man/man1
  cp share/man/man1/gfortran.1*                         ${pkgdir}${_pkg_prefix}/share/man/man1/
  mkdir -p ${pkgdir}${_pkg_prefix}/share/info
  cp share/info/gfortran.info*                          ${pkgdir}${_pkg_prefix}/share/info/
}

package_mingw-w64-gcc-ada() {
  pkgdesc="GNU Compiler Collection (Ada) for MinGW-w64"
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}=${pkgver}-${pkgrel}")

  mkdir -p ${pkgdir}${_pkg_prefix}/{bin,lib,share}

  cd ${srcdir}${_pkg_prefix}
  cp bin/gnat*.exe ${pkgdir}${_pkg_prefix}/bin/

  cp bin/{libgnarl*,libgnat*}.dll                       ${pkgdir}${_pkg_prefix}/bin/

  mkdir -p ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/adainclude     ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/adalib         ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/gnat1.exe         ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/

  mkdir -p ${pkgdir}${_pkg_prefix}/share/info
  cp share/info/gnat-style.info*                        ${pkgdir}${_pkg_prefix}/share/info/
  cp share/info/gnat_rm.info*                           ${pkgdir}${_pkg_prefix}/share/info/
  cp share/info/gnat_ugn.info*                          ${pkgdir}${_pkg_prefix}/share/info/
}

package_mingw-w64-gcc-objc() {
  pkgdesc="GNU Compiler Collection (ObjC,Obj-C++) for MinGW-w64"
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}=${pkgver}-${pkgrel}")

  mkdir -p ${pkgdir}${_pkg_prefix}/{bin,lib}

  cd ${srcdir}${_pkg_prefix}
  cp bin/libobjc*.dll ${pkgdir}${_pkg_prefix}/bin/

  mkdir -p ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/objc    ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/cc1obj.exe         ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/cc1objplus.exe     ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/libobjc.*                                       ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
}

# Wrappers for package functions

# 32-bit wrappers
package_mingw-w64-i686-gcc-libs() {
  _package_common
  package_mingw-w64-gcc-libs
}

package_mingw-w64-i686-gcc() {
  _package_common
  package_mingw-w64-gcc
}

package_mingw-w64-i686-gcc-libgfortran() {
  _package_common
  package_mingw-w64-gcc-libgfortran
}

package_mingw-w64-i686-gcc-fortran() {
  _package_common
  package_mingw-w64-gcc-fortran
}

package_mingw-w64-i686-gcc-ada() {
  _package_common
  package_mingw-w64-gcc-ada
}

package_mingw-w64-i686-gcc-objc() {
  _package_common
  package_mingw-w64-gcc-objc
}

# 64-bit wrappers
package_mingw-w64-x86_64-gcc-libs() {
  _package_common
  package_mingw-w64-gcc-libs
}

package_mingw-w64-x86_64-gcc() {
  _package_common
  package_mingw-w64-gcc
}

package_mingw-w64-x86_64-gcc-libgfortran() {
  _package_common
  package_mingw-w64-gcc-libgfortran
}

package_mingw-w64-x86_64-gcc-fortran() {
  _package_common
  package_mingw-w64-gcc-fortran
}

package_mingw-w64-x86_64-gcc-ada() {
  _package_common
  package_mingw-w64-gcc-ada
}

package_mingw-w64-x86_64-gcc-objc() {
  _package_common
  package_mingw-w64-gcc-objc
}
