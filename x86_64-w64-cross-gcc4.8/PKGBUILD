# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

_bare_gcc=yes

_threads="win32"
_build_prefix=/mingw32
_base_pkg_prefix=${_build_prefix}/opt
_mingw_suff=x86_64-w64-cross
_target="x86_64-w64-mingw32"
_build_and_host="i686-w64-mingw32"

_basename=gcc
_base_pkg_version=4.8
_realname=${_basename}
pkgbase=${_mingw_suff}-${_basename}${_base_pkg_version}
pkgname=("${_mingw_suff}-${_realname}")
pkgver=${_base_pkg_version}.2
pkgrel=1
pkgdesc="GCC for the MinGW-w64"
arch=('any')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=(
  "${MINGW_PACKAGE_PREFIX}-zlib"
  "${_mingw_suff}-binutils"
  $([[ "$_bare_gcc" != "yes" ]] && echo "${_mingw_suff}-crt")
  $([[ "$_bare_gcc" != "yes" ]] && echo "${_mingw_suff}-winpthreads")
  $([[ "$_bare_gcc" != "yes" ]] && echo "${_mingw_suff}-headers")
)
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" 'lndir' 'patch' 'make')
#checkdepends=('dejagnu')
optdepends=()
replaces=()
backup=()
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=("ftp://gcc.gnu.org/pub/gcc/releases/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.bz2"
    'gcc-4.8-filename-output.patch'
    'gcc-4.8-lambda-ICE.patch'
    'gcc-4.8-iconv.patch'
    '4.8-libgcc-references.patch'
    'gcc-4.8-libstdc++export.patch'
    'gcc-4.7-stdthreads.patch'
    '4.7-execstack.patch'
    '4.7-libffi-noinst.patch'
    '4.7-libitm-cygwin.patch'
    '4.7-libstdc-cygwin.patch'
    '4.8-libatomic-cygwin.patch'
    '4.8-libdecnumber-cygwin64.patch'
    '4.8-libffi-cygwin64.patch'
    '4.8-lto-plugin-soname.patch'
    '4.8-picflag-cygwin64.patch'
    '4.8-specs-cygwin.patch'
    'gcc-4.8.0-configure-msys2.patch'
    'gcc-4.8.0-msys2-spec.patch')
sha256sums=('09dc2276c73424bbbfda1dbddc62bbbf900c9f185acf7f3e1d773ce2d7e3cdc8'
            '9278e745190bac0b3f6ef74972aecd4e2891d4930f7b6fa8840c3d4f7fbf4f98'
            '301cfbe339043ca14205140acbec1d0f55178f01365ff309e4ddb90a3c934031'
            '9e0401c3e69317a45269fb689e4e89ee8f46997c2c52b11812a11c9faf142ede'
            '52ddd5b2d5f337efa7475de211eccebd70e343c902107343b22d66951fb799d3'
            '21191b4fd57ce5f230fcc97b4d9ae31bdc387d7c7c8e39436aa7e4268d278d3d'
            '5e0fc1437ce0b357e78d440692e3a30a7905a5f360a67928a95b14ec8d45365b'
            '4942e94ccdf11ce260e029400bd1cee8b63e78d071ac2bae812ba261edb7b67b'
            'af90eb0eb49e06b4735f428fee514c6b87f349c1d3669c8bc7bb6250d4126c4e'
            '1015575631397b588050e53aba5795cc8357e55a2754f8bc6214b564f38f5a06'
            '1be5f8d96073061628ffe761e953c9578355545d32ac7829165492ba5287d734'
            '6812f82299b33c5e354e8a54fcc96149957e9ffa35d576537923621d97c6817c'
            '889f14abf4d6f60e335b9d0f81b67bb97605193f0a584cd8b52eebf88425c55a'
            '7f3410ccc61f4b60ae9fc4201cc321f681771196e44fc61639bdf924a9132eec'
            '6e4322814f7b0f1d3ea3dbea0c8fa233d6ec9493354d7bd54e755ee971914d74'
            'c47cbc859d96844fa029e09abf060593a43acb908df586e1230daa70369cecae'
            'b9a4983feba55c0f70fadbf011fe1f7c6bc8390b1d12dc39c58986c6127bb482'
            'c620dafb8be2dc0cfc9a4242776f55b5c98b28397638ccbb16eb10512e76010f'
            '2106c9679c19a133ffffe64b9c950094695cf0d4e2609a90702f0d9e8bb9cfbc')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  #do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  #sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

  patch -p0 -i ${srcdir}/gcc-4.8-filename-output.patch
  patch -p1 -i ${srcdir}/gcc-4.8-lambda-ICE.patch
  #patch -p1 -i ${srcdir}/gcc-4.8-iconv.patch

  patch -p0 -i ${srcdir}/4.8-libgcc-references.patch
  patch -p1 -i ${srcdir}/gcc-4.8-libstdc++export.patch
  patch -p1 -i ${srcdir}/gcc-4.7-stdthreads.patch

  #MSYS/Cygwin patches
  patch -p2 -i ${srcdir}/4.7-execstack.patch
  patch -p2 -i ${srcdir}/4.8-libatomic-cygwin.patch
  patch -p2 -i ${srcdir}/4.8-libdecnumber-cygwin64.patch
  patch -p2 -i ${srcdir}/4.7-libffi-noinst.patch
  patch -p2 -i ${srcdir}/4.8-libffi-cygwin64.patch
  patch -p2 -i ${srcdir}/4.7-libitm-cygwin.patch
  patch -p2 -i ${srcdir}/4.7-libstdc-cygwin.patch
  patch -p2 -i ${srcdir}/4.8-lto-plugin-soname.patch
  patch -p2 -i ${srcdir}/4.8-picflag-cygwin64.patch
  patch -p0 -i ${srcdir}/4.8-specs-cygwin.patch
  patch -p1 -i ${srcdir}/gcc-4.8.0-configure-msys2.patch
  patch -p1 -i ${srcdir}/gcc-4.8.0-msys2-spec.patch
}

build() {
  mkdir -p ${srcdir}/gcc-build-${_target} && cd ${srcdir}/gcc-build-${_target}
  
  local -a configure_opts

  if [ "$_bare_gcc" == "yes" ]; then
    configure_opts+=("--disable-threads")
    configure_opts+=("--enable-languages=c")
  else
    configure_opts+=("--enable-threads=${_threads}")
    configure_opts+=("--enable-languages=c")
  fi

  ../${_realname}-${pkgver}/configure \
    --prefix=${_base_pkg_prefix} \
    --build=${_build_and_host} \
    --host=${_build_and_host} \
    --target=${_target} \
    --libexecdir=${_base_pkg_prefix}/lib \
    --disable-nls \
    --disable-bootstrap \
    --with-arch=x86-64 \
    --with-tune=generic \
    --enable-shared --enable-static \
    --disable-graphite \
    --disable-libstdcxx-pch \
    --disable-libstdcxx-debug \
    --disable-cloog-version-check --disable-isl-version-check \
    --disable-multilib \
    --disable-rpath \
    --disable-win32-registry \
    --disable-werror \
    --disable-symvers \
    --with-libiconv-prefix=${_build_prefix} \
    --with-system-zlib \
    --with-gnu-as --with-gnu-ld \
    CFLAGS="-O2 -pipe" \
    CXXFALGS="-O2 -pipe" \
    "${configure_opts[@]}"

  make all
}

package() {
    cd ${srcdir}/gcc-build-${_target}
    make -j1 DESTDIR=${pkgdir} install
    mv ${pkgdir}${_base_pkg_prefix}/lib/gcc/${_target}/lib/libgcc_s.a ${pkgdir}${_base_pkg_prefix}/lib/gcc/${_target}/${pkgver}/
    mv ${pkgdir}${_base_pkg_prefix}/lib/gcc/${_target}/libgcc*.dll ${pkgdir}${_base_pkg_prefix}/lib/gcc/${_target}/${pkgver}/
    rm -rf ${pkgdir}${_base_pkg_prefix}/share
}
