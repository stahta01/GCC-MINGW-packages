# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

_threads="win32"

_basename=gcc
_base_pkg_version=4.8
_realname=${_basename}
_mingw_suff=x86_64-w64-cross
pkgbase=${_mingw_suff}-${_basename}${_base_pkg_version}
pkgname=("${_mingw_suff}-${_realname}")
pkgver=${_base_pkg_version}.2
pkgrel=1
pkgdesc="GCC for the MinGW-w64"
arch=('i686' 'x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=("zlib" "mpc" "cloog" "${_mingw_suff}-crt" "${_mingw_suff}-binutils" "${_mingw_suff}-winpthreads" "${_mingw_suff}-headers")
makedepends=("gcc" 'lndir')
#checkdepends=('dejagnu')
optdepends=()
replaces=()
backup=()
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=("ftp://gcc.gnu.org/pub/gcc/releases/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.bz2"
    'gcc-4.8-filename-output.patch'
    'gcc-4.8-lambda-ICE.patch'
    'gcc-4.8-iconv.patch'
    '4.8-libgcc-references.patch'
    'gcc-4.8-libstdc++export.patch'
    'gcc-4.7-stdthreads.patch'
    '4.7-execstack.patch'
    '4.7-libffi-noinst.patch'
    '4.7-libitm-cygwin.patch'
    '4.7-libstdc-cygwin.patch'
    '4.8-libatomic-cygwin.patch'
    '4.8-libdecnumber-cygwin64.patch'
    '4.8-libffi-cygwin64.patch'
    '4.8-lto-plugin-soname.patch'
    '4.8-picflag-cygwin64.patch'
    '4.8-specs-cygwin.patch'
    'gcc-4.8.0-configure-msys2.patch'
    'gcc-4.8.0-msys2-spec.patch')
sha256sums=('a3d7d63b9cb6b6ea049469a0c4a43c9d'
         '40cb437805e2f7a006aa0d0c3098ab0f'
         '6eb6e080dbf7bc6825f53a0aaa6c4ef9'
         'd28f5653282c67bcfd1a8f81aebef72d'
         '928e151b33981e68d0e67a9bd387b37e'
         'f6b5fb08ff06a7d60ee3280e14240bb2'
         '71cdb550528efcf2a5c9112464bfbabc'
         'f7cb43c6a6a54e55027e70a9b03774b2'
         '15da8d111f8eab15597db353537aefd3'
         '094b047b34a044d4ba76074d98bfd8a0'
         'e553ecf0eeb3e83a149cc1241971fd10'
         'f01b3978cbd1f8aa14833a26a7891a32'
         '78eab6229fd809e5494df53797778e0b'
         '40b0a196ff005adb442ac5b9a2f42fd8'
         'fc7adee9dc8206aaf5e033798c358157'
         'acda6f3393d9e935953fa48f68e9d4f5'
         '3cbcce9b943b85562bb6afd7d83c5eac'
         'e0c65906da3c1cc14cd2158a9389be74'
         'c9fb65c97643807fcee2f21e897b5ed7')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  #do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  #sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure
  
  patch -p0 -i ${srcdir}/gcc-4.8-filename-output.patch
  patch -p1 -i ${srcdir}/gcc-4.8-lambda-ICE.patch
  #patch -p1 -i ${srcdir}/gcc-4.8-iconv.patch
  
  patch -p0 -i ${srcdir}/4.8-libgcc-references.patch
  patch -p1 -i ${srcdir}/gcc-4.8-libstdc++export.patch
  patch -p1 -i ${srcdir}/gcc-4.7-stdthreads.patch
  
  #MSYS/Cygwin patches
  patch -p2 -i ${srcdir}/4.7-execstack.patch
  patch -p2 -i ${srcdir}/4.8-libatomic-cygwin.patch
  patch -p2 -i ${srcdir}/4.8-libdecnumber-cygwin64.patch
  patch -p2 -i ${srcdir}/4.7-libffi-noinst.patch
  patch -p2 -i ${srcdir}/4.8-libffi-cygwin64.patch
  patch -p2 -i ${srcdir}/4.7-libitm-cygwin.patch
  patch -p2 -i ${srcdir}/4.7-libstdc-cygwin.patch
  patch -p2 -i ${srcdir}/4.8-lto-plugin-soname.patch
  patch -p2 -i ${srcdir}/4.8-picflag-cygwin64.patch
  patch -p0 -i ${srcdir}/4.8-specs-cygwin.patch
  patch -p1 -i ${srcdir}/gcc-4.8.0-configure-msys2.patch
  patch -p1 -i ${srcdir}/gcc-4.8.0-msys2-spec.patch
}

build() {
  mkdir -p ${srcdir}/gcc-build-x86_64-w64-mingw32 && cd ${srcdir}/gcc-build-x86_64-w64-mingw32

  ../${_realname}-${pkgver}/configure \
    --prefix=/mingw32/opt \
    --build=i686-w64-mingw32 \
    --host=i686-w64-mingw32 \
    --target=x86_64-w64-mingw32 \
    --libexecdir=/mingw32/opt/lib \
    --disable-bootstrap \
    --with-arch=x86-64 \
    --with-tune=generic \
    --enable-languages=c \
    --enable-shared --enable-static \
    --enable-threads=${_threads} \
    --disable-graphite \
    --disable-libstdcxx-pch \
    --disable-libstdcxx-debug \
    --disable-cloog-version-check --disable-isl-version-check \
    --disable-multilib \
    --disable-rpath \
    --disable-win32-registry \
    --disable-werror \
    --disable-symvers \
    --with-libiconv \
    --with-system-zlib \
    --with-gnu-as --with-gnu-ld \
    CFLAGS="-O2 -pipe" \
    CXXFALGS="-O2 -pipe"

  make all
}

package() {
    cd ${srcdir}/gcc-build-x86_64-w64-mingw32
    make -j1 DESTDIR=${pkgdir} install
    mv ${pkgdir}/mingw32/opt/lib/gcc/x86_64-w64-mingw32/lib/libgcc_s.a ${pkgdir}/mingw32/opt/lib/gcc/x86_64-w64-mingw32/${pkgver}/
    mv ${pkgdir}/mingw32/opt/lib/gcc/x86_64-w64-mingw32/libgcc*.dll ${pkgdir}/mingw32/opt/lib/gcc/x86_64-w64-mingw32/${pkgver}/
    rm -rf ${pkgdir}/mingw32/opt/share
}
