# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Alexey Borzenkov <snaury@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Liu Hao <lh_mouse@126.com>

_enable_info=no
_enable_bootstrap=no
_enable_threads=yes
_threads="posix"

_enable_gcc_plugin=no
_enable_gcc_lto=yes
_enable_ada=yes
_enable_cxx=no
_enable_d=no
_enable_fortran=no
_enable_jit=no
_enable_objc=no
_realname=gcc
pkgbase=mingw-w64-${_realname}4-git
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-libs-git"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-git"
         $([[ "$_enable_fortran" == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}-libgfortran-git")
         $([[ "$_enable_fortran" == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}-fortran-git")
         $([[ "$_enable_ada" == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}-ada-git")
         $([[ "$_enable_objc" == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}-objc-git")
        )
pkgver=4.7.4.d20131213+1.c2.g6cda24c97b5
pkgrel=1
pkgdesc="GCC for the MinGW-w64"
arch=('any')
url="https://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
makedepends=("${MINGW_PACKAGE_PREFIX}-${_realname}"
#             $([[ "$_enable_ada" == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}-ada")
             "${MINGW_PACKAGE_PREFIX}-binutils"
             "${MINGW_PACKAGE_PREFIX}-crt"
             "${MINGW_PACKAGE_PREFIX}-headers"
             "${MINGW_PACKAGE_PREFIX}-libiconv"
             "${MINGW_PACKAGE_PREFIX}-windows-default-manifest"
             "${MINGW_PACKAGE_PREFIX}-winpthreads"
             "${MINGW_PACKAGE_PREFIX}-zlib"
             'git' 'patch')
if [[ "$_enable_info" == "yes" ]]; then
  makedepends+=('texinfo')
fi
checkdepends=('dejagnu')
optdepends=()
options=('staticlibs' '!emptydirs') # '!strip' 'debug')

#_git_branch=releases/gcc-4.7                           # SVN211571
 _git_commit='6cda24c97b5a1fa6cd27365c9aa90c95f45da4aa' # SVN205956
#_git_commit='50bf9f145c75b1fbc372e33610af14a8be328e58' # SVN199341
#_git_commit='911595bf2d485c0d1e067d9a94a867d9b8281502' # SVN197738 releases/gcc-4.7.3
#_git_commit='b058453f82a5dab2304c3ddf1ce646eaa748a475' # SVN191557 releases/gcc-4.7.2
#_git_commit='4c4c82223c4c3c975acc829f51cfeef77835fd87' # SVN191070
#_git_commit='539f5de0732db5c5accb038cbfb07a8d5cb89599' # SVN190178 Revert SVN190174
#_git_commit='442a102d1884867d646eea51b30b92e5aa3bc6ae' # SVN189667
#_git_commit='bbbfa18796b1bdb65834cebc880664e3d78877d4' # SVN189200
#_git_commit='c979aa52203ca4a6d7f981c3150af3d44ff2d4a6' # SVN188654
#_git_commit='412d31f03a14be123bb01a7a33a93fd8a68e23e6' # SVN188623
#_git_commit='' # SVN
#_git_commit='' # SVN
#_git_commit='edc56cde5124be76dd6ae3baff9eeb1fb61a1c41' # SVN188596 releases/gcc-4.7.1 Built and installed okay; running it resulted in C++ preprocessor "/lib/cpp" fails sanity check
#_git_commit='050358f1954ebb05dbb5e0747ce01f3c65f8774b' # SVN188513
#_git_commit='3156520d5df2a2e52b78f266cd750aa05b5ae64c' # SVN188167
#_git_commit='95d2db2008d8ac8772cd0fd634e6228376e632eb' # SVN188122
#_git_commit='c8f5c98afa95c15641c97672de12df0664e564ae' # SVN187205
#_git_commit='c45a86fd95f3afecb6dc2dc99232062845f4b570' # SVN186959

#_git_repo_url=git://gcc.gnu.org/git/gcc.git
#_sourcedir=${_realname}-git
#_git_repo_url=git://github.com/stahta01/gcc.git
#_sourcedir=my-${_realname}-git
_git_repo_url=git://github.com/stahta01/gcc_main_development.git
_sourcedir=main-${_realname}-git

if [ -n "$_git_branch" ]; then
  source=(${_sourcedir}::"${_git_repo_url}#branch=$_git_branch")
elif [ -n "$_git_tag" ]; then
  source=(${_sourcedir}::"${_git_repo_url}#tag=$_git_tag")
elif [ -n "$_git_commit" ]; then
  source=(${_sourcedir}::"${_git_repo_url}#commit=$_git_commit")
else
  source=(${_sourcedir}::"${_git_repo_url}")
fi
MPFR_VERSION=mpfr-2.4.2
GMP_VERSION=gmp-4.3.2
MPC_VERSION=mpc-0.8.1
noextract=("${MPFR_VERSION}.tar.bz2" "${GMP_VERSION}.tar.bz2" "${MPC_VERSION}.tar.gz")
source+=("ftp://gcc.gnu.org/pub/gcc/infrastructure/${MPFR_VERSION}.tar.bz2"
        "ftp://gcc.gnu.org/pub/gcc/infrastructure/${GMP_VERSION}.tar.bz2"
        "ftp://gcc.gnu.org/pub/gcc/infrastructure/${MPC_VERSION}.tar.gz")
source+=(
  '001-gcc-4.8-libstdc++export.patch'
  '002-gcc-4.7-stdthreads.patch'
  '130-dont-escape-arguments-that-dont-need-it-in-pex-win32.c.patch'
  '140-fix-for-windows-not-minding-non-existant-parent-dirs.patch'
  '150-windows-lrealpath-no-force-lowercase-nor-backslash.patch'
  '160-mingw-dont-ignore-native-system-header-dir.patch'
  '210-gcc-4.4-ktietz-libgomp.patch'
  '230-build-more-gnattools.mingw.patch'
  '802-handle-use-mingw-ansi-stdio.patch'
  '804-gcc-4.6.4-Fix-texi-docs-syntax-errors.patch'
  '806-gcc-4.8-libgcc-references.patch'
  '807-gcc-4.8-lambda-ICE.patch'
  '808-gcc-4.7.0-SVN-196003-PR-target-52122-add-LN_S_RECURSIVE.patch')
sha256sums=('SKIP'
            'c7e75a08a8d49d2082e4caee1591a05d11b9d5627514e678f02d66a124bcf2ba'
            '936162c0312886c21581002b79932829aa048cfaf9937c6265aeaa14f1cd1775'
            'e664603757251fd8a352848276497a4c79b7f8b21fd8aedd5cc0598a38fee3e4'
            '21191b4fd57ce5f230fcc97b4d9ae31bdc387d7c7c8e39436aa7e4268d278d3d'
            '5e0fc1437ce0b357e78d440692e3a30a7905a5f360a67928a95b14ec8d45365b'
            '800e81e4e6acb07f1b69cccc07a8dae8cdbf7c084ac00c8869e9b4cb205c655c'
            'd341bfb0e655f7daf0057aab899c3ac9e1363dbb6f0ca2cbffa6144e0ca2ad17'
            '0953d65d4fb3d681c76b6d6353e3cc424751a417872435f724d463001da66d43'
            'a7c5daced21086a01b3af9af4c46ee738ccfca6e67b3413771cb7ab81aa2a83f'
            'efd0d90eda577db320f1ed704efa5be87c4da09f1c95c3d067e0b563e477e579'
            'd16e76e0aa9e5a1c239424edc3a6da52785e82849b6e00850f8e2526e7f496d9'
            '3b9779b05198b4c814709f211623c0b1ebf8937f32eba29826cf15bfc29df9a3'
            '2cafd273a667f1130e9cd20c87cd5c83cda1100de9958b9dbbf656dcf29fd2bb'
            'cdedc021d5389e59066e6bbc0b053a7fd23386d7aace8f40a4c3f53045ef36a3'
            'dbbb202cd36aea01f3671997d4e53aad6674de5b774812d6a6460e58997a8117'
            'ceb22c62c285b550f8a6d65d8a30580e2aa0e283b80fb1586665d6ce37181d34')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    if patch --dry-run -Nbp1 -i "${srcdir}/${_patch}" > /dev/null 2>&1 ; then
      msg2 "Applying ${_patch}"
      patch -Nbp1 -i "${srcdir}/${_patch}"
    elif patch --dry-run -Rbp1 -i "${srcdir}/${_patch}" > /dev/null 2>&1 ; then
      msg2 "Skipping ${_patch} because it likely was already applied"
    else
      msg2 "Skipping ${_patch} because it likely will fail"
    fi
  done
}

_extract_to_source_folder() {
    local tarfile="$1"
    local subfolder="$(echo "$tarfile" | sed 's/-.*$//')"
    if [ ! -d  "${srcdir}/${_sourcedir}/$subfolder" ]; then
        echo "Extracting ${tarfile} to ${_sourcedir}/$subfolder"
        mkdir -p "${srcdir}/${_sourcedir}/$subfolder"
        tar -x --strip-components=1 -f "${srcdir}/$tarfile" -C "${srcdir}/${_sourcedir}/$subfolder"
    fi
}
# =========================================== #

_git_base_commit=
_gcc_version=
_gcc_date=

pkgver() {
  cd ${srcdir}/${_sourcedir}
  printf "%s.d%s+%s.c%s.g%s" "$_gcc_version" "$_gcc_date" $(git rev-list --count $(git rev-list -1 ${_git_base_commit} gcc/DATESTAMP)..${_git_base_commit}) $(git rev-list --count ${_git_base_commit}..HEAD) $(git rev-parse --short ${_git_base_commit})
}

prepare() {
  cd ${srcdir}/${_sourcedir}
  _git_base_commit=$(git rev-parse HEAD)
  _gcc_version=$(head -n 34 gcc/BASE-VER | sed -e 's/.* //' | tr -d '"\n')
  _gcc_date=$(head -n 34 gcc/DATESTAMP | sed -e 's/.* //' | tr -d '"\n')

  echo "_gcc_version := ${_gcc_version}"
  echo "_gcc_date := ${_gcc_date}"

  _extract_to_source_folder       $MPFR_VERSION.tar.bz2
  _extract_to_source_folder       $GMP_VERSION.tar.bz2
  _extract_to_source_folder       $MPC_VERSION.tar.gz


  cd ${srcdir}/${_sourcedir}
  GIT_AM="git am --committer-date-is-author-date"

  apply_patch_with_msg 001-gcc-4.8-libstdc++export.patch
  apply_patch_with_msg 002-gcc-4.7-stdthreads.patch

  # Don't waste valuable commandline chars on double-quotes around "arguments"
  #  that don't need them.
  apply_patch_with_msg 130-dont-escape-arguments-that-dont-need-it-in-pex-win32.c.patch

  # Make Windows behave the same as Posix in the consideration of whether folder
  # "/exists/doesnt-exist/.." is a valid path.. in Posix, it isn't.
  apply_patch_with_msg 140-fix-for-windows-not-minding-non-existant-parent-dirs.patch

  # Don't make a lowercase backslashed path from argv[0]
  #  that then fail to strcmp with prefix(es) .. they're also ugly.
  apply_patch_with_msg 150-windows-lrealpath-no-force-lowercase-nor-backslash.patch

  # Don't ignore values passed via --with-native-system-header-dir=
  # apply_patch_with_msg 160-mingw-dont-ignore-native-system-header-dir.patch

  ${GIT_AM} ${srcdir}/210-gcc-4.4-ktietz-libgomp.patch

  # Build extra tools for Ada
  apply_patch_with_msg 230-build-more-gnattools.mingw.patch

  apply_patch_with_msg 802-handle-use-mingw-ansi-stdio.patch
  ${GIT_AM} ${srcdir}/804-gcc-4.6.4-Fix-texi-docs-syntax-errors.patch
  apply_patch_with_msg 806-gcc-4.8-libgcc-references.patch
  apply_patch_with_msg 807-gcc-4.8-lambda-ICE.patch
  apply_patch_with_msg 808-gcc-4.7.0-SVN-196003-PR-target-52122-add-LN_S_RECURSIVE.patch

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

  # do not expect $prefix/mingw symlink - this should be superceded by
  # Windows-Don-t-ignore-native-system-header-dir.patch .. but isn't!
  sed -i 's/${prefix}\/mingw\//${prefix}\//g' configure

  # change hardcoded /mingw prefix to the real prefix .. isn't this rubbish?
  # it might work at build time and could be important there but beyond that?!
  local MINGW_NATIVE_PREFIX=$(cygpath -am ${MINGW_PREFIX}/${MINGW_CHOST})
  sed -i "s#\\/mingw\\/#${MINGW_NATIVE_PREFIX//\//\\/}\\/#g" gcc/config/i386/mingw32.h
}

_package_common() {
  # use with "with-gcc-major-version-only" configuration option
  #_gcc_driver_version=`eval "sed -e 's/^\([0-9]*\).*/\1/' ${srcdir}/${_sourcedir}/gcc/BASE-VER"`

  # Use without "with-gcc-major-version-only" configuration option
  _gcc_driver_version=`eval "cat ${srcdir}/${_sourcedir}/gcc/BASE-VER"`

  echo "_gcc_driver_version := ${_gcc_driver_version}"

  _pkg_prefix=${MINGW_PREFIX}
  #_pkg_prefix=/opt${MINGW_PREFIX}/${_realname}

  echo "_pkg_prefix := ${_pkg_prefix}"
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  _package_common

  ${MINGW_PREFIX}/bin/gcc --version
  ${MINGW_PREFIX}/bin/ld  --version
  if [ "$_enable_ada" == "yes" ]; then
    ${MINGW_PREFIX}/bin/gnatmake  --version
  fi

  local -a configure_opts

  case "${CARCH}" in
    i686)
      configure_opts+=("--disable-sjlj-exceptions")
      configure_opts+=("--with-dwarf2")
      LDFLAGS+=" -Wl,--large-address-aware"
      local _arch=i686
    ;;

    x86_64)
      local _arch=x86-64
    ;;
  esac

  if [ "$_enable_bootstrap" == "yes" ]; then
    configure_opts+=("--enable-bootstrap")
  elif [ "$_enable_bootstrap" == "no" ]; then
    configure_opts+=("--disable-bootstrap")
  fi

  if [ "$_enable_gcc_lto" == "yes" ]; then
    configure_opts+=("--enable-lto")
  else
    configure_opts+=("--disable-lto")
  fi

  if [ "$_enable_gcc_plugin" == "yes" ]; then
    configure_opts+=("--enable-plugin")
  else
    configure_opts+=("--disable-plugin")
  fi

  if [[ ${_gcc_version:1:1} == "." && ${_gcc_version:0:1} != "9" ]]; then
    configure_opts+=("--enable-libstdcxx-filesystem-ts=no")
  fi

  local _languages="c"
  if [ "$_enable_gcc_lto" == "yes" ]; then
    _languages+=",lto"
  fi
  if [ "$_enable_d" == "yes" ]; then
    _languages+=",d"
  fi
  if [ "$_enable_fortran" == "yes" ]; then
    _languages+=",fortran"
  fi
  if [ "$_enable_ada" == "yes" ]; then
    _languages+=",ada"
  fi
  if [ "$_enable_cxx" == "yes" ]; then
    _languages+=",c++"
  fi
  if [ "$_enable_objc" == "yes" ]; then
    _languages+=",objc,obj-c++"
  fi
  if [ "$_enable_jit" == "yes" ]; then
    _languages+=",jit"
  fi

  echo "CPPFLAGS := ${CPPFLAGS}"
  echo "CFLAGS := ${CFLAGS}"
  echo "CXXFLAGS := ${CXXFLAGS}"
  echo "LDFLAGS := ${LDFLAGS}"

  ../${_sourcedir}/configure \
    --prefix=${_pkg_prefix} \
    --with-local-prefix=${MINGW_PREFIX}/local \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-native-system-header-dir=${MINGW_PREFIX}/${MINGW_CHOST}/include \
    --with-gxx-include-dir=${MINGW_PREFIX}/include/c++/${pkgver} \
    --enable-version-specific-runtime-libs \
    --libexecdir=${_pkg_prefix}/lib \
    --with-arch=${_arch} \
    --with-tune=generic \
    --enable-languages=${_languages} \
    --enable-shared --enable-static \
    --enable-threads=${_threads} \
    --enable-graphite \
    --enable-fully-dynamic-string \
    --enable-libstdcxx-time=yes \
    --disable-libstdcxx-pch \
    --disable-libstdcxx-debug \
    --enable-libgomp \
    --disable-multilib \
    --enable-checking=release \
    --disable-rpath \
    --disable-win32-registry \
    --disable-nls \
    --disable-werror \
    --disable-symvers \
    --without-cloog --without-isl \
    --with-libiconv \
    --with-system-zlib \
    --with-pkgversion="Rev${pkgrel}, Built by stahta01 -- Tim S" \
    --with-bugurl="https://github.com/stahta01/GCC-MINGW-packages/issues" \
    --with-gnu-as --with-gnu-ld \
    "${configure_opts[@]}"

  # While we're debugging -fopenmp problems at least.
  # .. we may as well not strip anything.
  if check_option "strip" "n"; then
    sed -i 's,^STRIP = .*$,STRIP = true,g'                   Makefile
    sed -i 's,^STRIP_FOR_TARGET=.*$,STRIP_FOR_TARGET=true,g' Makefile
  fi

  make -j1 all

  rm -rf ${srcdir}/install-${CARCH}${_pkg_prefix}
  mkdir -p ${srcdir}/install-${CARCH}

  make -j1 DESTDIR=${srcdir}/install-${CARCH} install
  if [ "$_enable_ada" == "yes" ]; then
    mv ${srcdir}/install-${CARCH}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/adalib/*.dll ${srcdir}/install-${CARCH}${_pkg_prefix}/bin/
  fi
}

check() {
  #cd ${srcdir}/build-${MINGW_CHOST}/gcc
  #if [ "$_enable_gcc_lto" == "yes" ]; then
    #make -j1 -k check-lto || true
  #fi
  cd ${srcdir}/build-${MINGW_CHOST}
  ##make -j1 -k check-c++ || true
  ##make -j1 -k check-c   || true
  if [ "$_enable_ada" == "yes" ]; then
    make -j1 -k check-ada || true
  fi
}

package_mingw-w64-gcc-libs-git() {
  pkgdesc="GNU Compiler Collection (libraries) for MinGW-w64"
  depends=()
  if [[ "$_enable_threads" == "yes" && "$_threads" == "posix" ]]; then
    depends+=("${MINGW_PACKAGE_PREFIX}-libwinpthread")
  fi
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}-libs")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-libs")

  # Licensing information

  # Part of the package is GCCRLE, part is LGPL2+, see README generation below.
  # Since the packaged GCCRLE libraries are also GPL3+, and LGPL2+ is compatible
  # with GPL3+, the whole package can be redistributed under GPL3+.
  license=(GPL3+ partial:'GCCRLE' partial:'LGPL2+')

  # We explain the licensing in this generated README file
  mkdir -p "${pkgdir}${_pkg_prefix}/share/licenses/${_realname}-libs"
  cat << ENDFILE > "${pkgdir}${_pkg_prefix}/share/licenses/${_realname}-libs/README"
The libgcc, libssp, libstdc++, libgomp and libatomic libraries are covered by
GPL3+ with the GCC Runtime Library Exception. The libquadmath library is covered
by LGPL2+. The package as a whole can be redistributed under GPL3+.
ENDFILE

  # License files
  install -Dm644 "${srcdir}/${_sourcedir}/COPYING3"        "${pkgdir}${_pkg_prefix}/share/licenses/${_realname}-libs/COPYING3"
  install -Dm644 "${srcdir}/${_sourcedir}/COPYING.LIB"     "${pkgdir}${_pkg_prefix}/share/licenses/${_realname}-libs/COPYING.LIB"
  install -Dm644 "${srcdir}/${_sourcedir}/COPYING.RUNTIME" "${pkgdir}${_pkg_prefix}/share/licenses/${_realname}-libs/COPYING.RUNTIME"

  mkdir -p ${pkgdir}${_pkg_prefix}/bin

  cd ${srcdir}/install-${CARCH}${_pkg_prefix}
  # libitm* and libvtv* are disbled until fixed
  cp bin/{libgcc*,libgomp*,libquadmath*,libssp*}.dll ${pkgdir}${_pkg_prefix}/bin/
  if [ "$_enable_cxx" == "yes" ]; then
    cp bin/libstdc*.dll ${pkgdir}${MINGW_PREFIX}/bin/
  fi
  if [ "$_enable_jit" == "yes" ]; then
    rm ${pkgdir}${_pkg_prefix}/bin/libgccjit-0.dll
  fi
}

package_mingw-w64-gcc-git() {
  pkgdesc="GNU Compiler Collection (C,C++,OpenMP) for MinGW-w64"
  depends=("${MINGW_PACKAGE_PREFIX}-binutils"
           "${MINGW_PACKAGE_PREFIX}-crt"
           "${MINGW_PACKAGE_PREFIX}-headers"
           "${MINGW_PACKAGE_PREFIX}-libiconv"
           "${MINGW_PACKAGE_PREFIX}-${_realname}-libs-git=${pkgver}-${pkgrel}"
           "${MINGW_PACKAGE_PREFIX}-windows-default-manifest"
           "${MINGW_PACKAGE_PREFIX}-winpthreads"
           "${MINGW_PACKAGE_PREFIX}-zlib")
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  mkdir -p ${pkgdir}${_pkg_prefix}/{bin,include,lib,share}

  cd ${srcdir}/install-${CARCH}${_pkg_prefix}
  cp bin/cpp.exe                                        ${pkgdir}${_pkg_prefix}/bin/
  cp bin/gcc.exe                                        ${pkgdir}${_pkg_prefix}/bin/
  cp bin/gcc.exe                                        ${pkgdir}${_pkg_prefix}/bin/cc.exe
  cp bin/gcc-ar.exe                                     ${pkgdir}${_pkg_prefix}/bin/
  cp bin/gcc-nm.exe                                     ${pkgdir}${_pkg_prefix}/bin/
  cp bin/gcc-ranlib.exe                                 ${pkgdir}${_pkg_prefix}/bin/
  cp bin/gcov.exe                                       ${pkgdir}${_pkg_prefix}/bin/
  if [ "$_enable_cxx" == "yes" ]; then
    cp bin/c++.exe                                        ${pkgdir}${_pkg_prefix}/bin/
    cp bin/g++.exe                                        ${pkgdir}${_pkg_prefix}/bin/
    cp bin/${MINGW_CHOST}-c++.exe                         ${pkgdir}${_pkg_prefix}/bin/
    cp bin/${MINGW_CHOST}-g++.exe                         ${pkgdir}${_pkg_prefix}/bin/
  fi
  cp bin/${MINGW_CHOST}-gcc-${_gcc_driver_version}.exe          ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-gcc.exe                         ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-gcc-ar.exe                      ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-gcc-nm.exe                      ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-gcc-ranlib.exe                  ${pkgdir}${_pkg_prefix}/bin/

  mkdir -p ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/*.h        ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/ssp     ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include-fixed   ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/install-tools   ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  if [ "$_enable_gcc_plugin" == "yes" ]; then
    cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/plugin          ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  fi
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/cc1.exe            ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/collect2.exe       ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/crt*.o             ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  if [ "$_enable_gcc_lto" == "yes" ]; then
    cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/liblto*            ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  fi
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/libgcov*           ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/lto*.exe           ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/

  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/libgcc*            ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/

  if [ "$_enable_cxx" == "yes" ]; then
    cp -r include/c++                                               ${pkgdir}${_pkg_prefix}/include/
    cp    lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/cc1plus.exe ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
    cp    lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/libstdc++*  ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
    cp    lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/libsupc++*  ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  fi

  if [ "$_enable_cxx" == "yes" ]; then
    mkdir -p ${pkgdir}${_pkg_prefix}/share/gcc-${_gcc_driver_version}/python
    cp -r share/gcc-${_gcc_driver_version}/python/libstdcxx  ${pkgdir}${_pkg_prefix}/share/gcc-${_gcc_driver_version}/python/
  fi

  mkdir -p ${pkgdir}${_pkg_prefix}/share/{doc,info,locale,man}
  #cp -r share/doc/gcc-${_gcc_driver_version} ${pkgdir}${_pkg_prefix}/share/doc/
  if [ "$_enable_info" == "yes" ]; then
    cp share/info/cpp.info*                                ${pkgdir}${_pkg_prefix}/share/info/
    cp share/info/cppinternals.info*                       ${pkgdir}${_pkg_prefix}/share/info/
    cp share/info/gcc.info*                                ${pkgdir}${_pkg_prefix}/share/info/
    cp share/info/gccinstall.info*                         ${pkgdir}${_pkg_prefix}/share/info/
    cp share/info/gccint.info*                             ${pkgdir}${_pkg_prefix}/share/info/
    #cp share/info/libitm.info*                             ${pkgdir}${_pkg_prefix}/share/info/
    cp share/info/libgomp.info*                            ${pkgdir}${_pkg_prefix}/share/info/
    cp share/info/libquadmath.info*                        ${pkgdir}${_pkg_prefix}/share/info/
  fi

  #cp share/locale/* ${pkgdir}${_pkg_prefix}/share/locale/
  mkdir -p ${pkgdir}${_pkg_prefix}/share/man/man1
  cp share/man/man1/cpp.1*                               ${pkgdir}${_pkg_prefix}/share/man/man1/
  cp share/man/man1/gcc.1*                               ${pkgdir}${_pkg_prefix}/share/man/man1/
  cp share/man/man1/gcov.1*                              ${pkgdir}${_pkg_prefix}/share/man/man1/
  cp share/man/man7/fsf-funding.7*                       ${pkgdir}${_pkg_prefix}/share/man/man1/
  cp share/man/man7/gfdl.7*                              ${pkgdir}${_pkg_prefix}/share/man/man1/
  cp share/man/man7/gpl.7*                               ${pkgdir}${_pkg_prefix}/share/man/man1/
  if [ "$_enable_cxx" == "yes" ]; then
    cp share/man/man1/g++.1*                               ${pkgdir}${_pkg_prefix}/share/man/man1/
  fi
}

package_mingw-w64-gcc-libgfortran-git() {
  pkgdesc="GNU Compiler Collection (libgfortran) for MinGW-w64"
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}-libs-git=${pkgver}-${pkgrel}")
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}-libgfortran")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-libgfortran")

  mkdir -p ${pkgdir}${_pkg_prefix}/bin
  cd ${srcdir}/install-${CARCH}${_pkg_prefix}
  cp bin/libgfortran*.dll                                ${pkgdir}${_pkg_prefix}/bin/
}

package_mingw-w64-gcc-fortran-git() {
  pkgdesc="GNU Compiler Collection (Fortran) for MinGW-w64"
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}-git=${pkgver}-${pkgrel}"
           "${MINGW_PACKAGE_PREFIX}-${_realname}-libgfortran-git=${pkgver}-${pkgrel}")
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}-fortran")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-fortran")

  mkdir -p ${pkgdir}${_pkg_prefix}/{bin,lib,share}

  cd ${srcdir}/install-${CARCH}${_pkg_prefix}
  cp bin/gfortran.exe ${pkgdir}${_pkg_prefix}/bin/
  cp bin/${MINGW_CHOST}-gfortran.exe ${pkgdir}${_pkg_prefix}/bin/

  mkdir -p ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/finclude       ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/f951.exe          ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/libcaf_single.a   ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/libgfortran*                                        ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  mkdir -p ${pkgdir}${_pkg_prefix}/share/man/man1
  cp share/man/man1/gfortran.1*                         ${pkgdir}${_pkg_prefix}/share/man/man1/
  if [ "$_enable_info" == "yes" ]; then
    mkdir -p ${pkgdir}${_pkg_prefix}/share/info
    cp share/info/gfortran.info*                          ${pkgdir}${_pkg_prefix}/share/info/
  fi
}

package_mingw-w64-gcc-ada-git() {
  pkgdesc="GNU Compiler Collection (Ada) for MinGW-w64"
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}-git=${pkgver}-${pkgrel}")
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}-ada")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-ada")

  mkdir -p ${pkgdir}${_pkg_prefix}/{bin,lib,share}

  cd ${srcdir}/install-${CARCH}${_pkg_prefix}
  cp bin/gnat*.exe ${pkgdir}${_pkg_prefix}/bin/

  cp bin/{libgnarl*,libgnat*}.dll                       ${pkgdir}${_pkg_prefix}/bin/

  mkdir -p ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/adainclude     ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/adalib         ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/gnat1.exe         ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/

  if [ "$_enable_info" == "yes" ]; then
    mkdir -p ${pkgdir}${_pkg_prefix}/share/info
    cp share/info/gnat-style.info*                        ${pkgdir}${_pkg_prefix}/share/info/
    cp share/info/gnat_rm.info*                           ${pkgdir}${_pkg_prefix}/share/info/
    cp share/info/gnat_ugn.info*                          ${pkgdir}${_pkg_prefix}/share/info/
  fi
}

package_mingw-w64-gcc-objc-git() {
  pkgdesc="GNU Compiler Collection (ObjC,Obj-C++) for MinGW-w64"
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}-git=${pkgver}-${pkgrel}")
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}-objc")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-objc")

  mkdir -p ${pkgdir}${_pkg_prefix}/{bin,lib}

  cd ${srcdir}/install-${CARCH}${_pkg_prefix}
  cp bin/libobjc*.dll ${pkgdir}${_pkg_prefix}/bin/

  mkdir -p ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include
  cp -r lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/objc    ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/include/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/cc1obj.exe         ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/cc1objplus.exe     ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
  cp lib/libobjc.*                                            ${pkgdir}${_pkg_prefix}/lib/gcc/${MINGW_CHOST}/${_gcc_driver_version}/
}

# Wrappers for package functions

# 32-bit wrappers
package_mingw-w64-i686-gcc-libs-git() {
  _package_common
  package_mingw-w64-gcc-libs-git
}

package_mingw-w64-i686-gcc-git() {
  _package_common
  package_mingw-w64-gcc-git
}

package_mingw-w64-i686-gcc-libgfortran-git() {
  _package_common
  package_mingw-w64-gcc-libgfortran-git
}

package_mingw-w64-i686-gcc-fortran-git() {
  _package_common
  package_mingw-w64-gcc-fortran-git
}

package_mingw-w64-i686-gcc-ada-git() {
  _package_common
  package_mingw-w64-gcc-ada-git
}

package_mingw-w64-i686-gcc-objc-git() {
  _package_common
  package_mingw-w64-gcc-objc-git
}

# 64-bit wrappers
package_mingw-w64-x86_64-gcc-libs-git() {
  _package_common
  package_mingw-w64-gcc-libs-git
}

package_mingw-w64-x86_64-gcc-git() {
  _package_common
  package_mingw-w64-gcc-git
}

package_mingw-w64-x86_64-gcc-libgfortran-git() {
  _package_common
  package_mingw-w64-gcc-libgfortran-git
}

package_mingw-w64-x86_64-gcc-fortran-git() {
  _package_common
  package_mingw-w64-gcc-fortran-git
}

package_mingw-w64-x86_64-gcc-ada-git() {
  _package_common
  package_mingw-w64-gcc-ada-git
}

package_mingw-w64-x86_64-gcc-objc-git() {
  _package_common
  package_mingw-w64-gcc-objc-git
}
